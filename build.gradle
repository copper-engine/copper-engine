import java.nio.charset.StandardCharsets

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    // the license plugin, see https://github.com/hierynomus/license-gradle-plugin
    id "com.github.hierynomus.license" version "0.16.1"
    id "com.github.spotbugs" version "6.4.3"
    id "com.github.ben-manes.versions" version "0.53.0"
    id "com.gradleup.nmcp" version "1.2.0"
    id "com.gradleup.nmcp.aggregation" version "1.2.0"
}

wrapper {
    gradleVersion = '9.1.0'
    distributionType = Wrapper.DistributionType.ALL
}

apply plugin: 'maven-publish'
nmcpAggregation {
    centralPortal {
        username = System.getenv("SONA_TOKEN_USERNAME")
        password = System.getenv("SONA_TOKEN_PASSWORD")
        publishingType = "USER_MANAGED"
    }
}

dependencies {
    nmcpAggregation(project(":projects:copper-cassandra"))
    nmcpAggregation(project(":projects:copper-coreengine"))
    nmcpAggregation(project(":projects:copper-ext"))
    nmcpAggregation(project(":projects:copper-jmx-interface"))
    nmcpAggregation(project(":projects:copper-performance-test"))
    nmcpAggregation(project(":projects:copper-regtest"))
    nmcpAggregation(project(":projects:copper-spring"))
}


allprojects {
    group = "org.copper-engine"

    repositories {
        mavenCentral()
    }
}

def getProperSubprojects() {
    subprojects.findAll {
        new File(it.projectDir, 'src/main/java').directory
    }
}

configure(properSubprojects) {
    apply plugin: 'java-library'
    apply plugin: "com.github.spotbugs"
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: "com.gradleup.nmcp"
    apply plugin: 'com.github.hierynomus.license'


    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }
    compileJava.options.encoding = StandardCharsets.UTF_8
    compileTestJava.options.encoding = StandardCharsets.UTF_8

    tasks.register('createSourcesJar', Jar) {
        dependsOn classes
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.register('createJavadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives createSourcesJar
        archives createJavadocJar
    }

    license {
        // verify that every java file has our Apache License header; fail build if header is missing
        header file("$rootDir/common/apache-license-file.txt")
        skipExistingHeaders true
        ignoreFailures true
    }

    dependencies {
        implementation("com.github.javaparser:javaparser-symbol-solver-core:$javaparserVersion") {
            exclude module: 'javaparser-symbol-solver-model'
        }

        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "net.bytebuddy:byte-buddy:$byteBuddyVersion"
        testImplementation "org.hamcrest:hamcrest-core:$hamcrestVersion"
        testImplementation "ch.qos.logback:logback-classic:$logbackVersion"

    }

    jar.doFirst {
        manifest {
            attributes("Created-By": 'SCOOP Software GmbH',
                    "Implementation-Title": "COPPER",
                    "Implementation-Version": project.findProperty('version'),
                    "Implementation-Vendor": 'SCOOP Software GmbH',
                    "Automatic-Module-Name": project.moduleName)
        }
    }

    javadoc {
        options.encoding = StandardCharsets.UTF_8
        options.addBooleanOption('html5', true)
    }

    spotbugs {
        toolVersion = '4.8.6'
        ignoreFailures = true
    }

    publishing {

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact createSourcesJar
                artifact createJavadocJar
                pom {
                    name = 'COPPER high-performance workflow engine'
                    packaging = 'jar'
                    description = 'COPPER is an open-source, powerful, light-weight, and easily configurable workflow engine. The power of COPPER is that it uses Java as a description language for workflows.'
                    url = 'http://copper-engine.org/'

                    scm {
                        url = 'https://github.com/copper-engine/copper-engine'
                        connection = 'scm:git@github.com:copper-engine/copper-engine.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'copper-team'
                            name = 'Copper Engine Development Team'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    test {
        useJUnitPlatform()
        minHeapSize = "128m"
        maxHeapSize = "4g"
        ignoreFailures = true
        jvmArgs += ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

project(':projects:copper-jmx-interface') {
    ext.moduleName = 'org.copperengine.management'
    dependencies {
    }
}


project(':projects:copper-regtest') {
    ext.moduleName = 'org.copperengine.regtest'

    sourceSets {
        test.java {
            srcDir file('src/workflow/java')
        }
    }

    dependencies {
        implementation project(':projects:copper-coreengine')
        implementation project(':projects:copper-spring')
        implementation project(':projects:copper-ext')

        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "org.yaml:snakeyaml:$snakeyamlVersion"
        implementation "org.springframework:spring-jdbc:$springVersion"
        implementation "org.springframework:spring-context:$springVersion"
        implementation "org.springframework:spring-tx:$springVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        testRuntimeOnly fileTree(dir: "$rootDir/3rdPartyLibs", include: '*.jar')

        testImplementation project(':projects:copper-performance-test')
        testImplementation "mysql:mysql-connector-java:$mysqlVersion"
        testImplementation "org.apache.derby:derby:$derbyVersion"
        testImplementation "postgresql:postgresql:$postgresqlVersion"
        testImplementation "com.h2database:h2:$h2Version"
        testImplementation "com.mchange:c3p0:$c3p0Version"

        testImplementation "org.slf4j:slf4j-api:$slf4jVersion"

    }
}

project(':projects:copper-ext') {
    ext.moduleName = 'org.copperengine.ext'
    dependencies {
        implementation project(':projects:copper-coreengine')

        implementation "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "commons-io:commons-io:$commonsIoVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "org.yaml:snakeyaml:$snakeyamlVersion"

        testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
        //testImplementation 'org.apache.logging.log4j:log4j-core:2.+'
        //testImplementation 'org.slf4j:slf4j-log4j12:2.+'
    }
}

project(':projects:copper-cassandra:cassandra-storage') {
    ext.moduleName = 'org.copperengine.cassandra.storage'
    dependencies {
        implementation project(':projects:copper-coreengine')
        implementation project(':projects:copper-ext')

        api "org.apache.cassandra:cassandra-all:$cassandraAllVersion"
        api "com.google.guava:guava:$guavaVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "commons-io:commons-io:$commonsIoVersion"
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        implementation "org.yaml:snakeyaml:$snakeyamlVersion"

        testImplementation("org.cassandraunit:cassandra-unit:$cassandraUnitVersion") {
            exclude module: 'hamcrest-core'
            exclude module: 'hamcrest-library'
            exclude module: 'cassandra-thrift'
            exclude module: 'high-scale-lib'
            exclude module: 'junit'
        }
    }
}

project(':projects:copper-cassandra:cassandra-loadtest') {
    apply plugin: 'application'
    ext.moduleName = 'org.copperengine.cassandra.loadtest'

    application {
        mainClass = "org.copperengine.core.persistent.cassandra.loadtest.PermanentLoadCreator"
    }

    dependencies {
        implementation project(':projects:copper-coreengine')
        implementation project(':projects:copper-ext')
        implementation project(':projects:copper-cassandra:cassandra-storage')

        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "org.yaml:snakeyaml:$snakeyamlVersion"
    }
}


project(':projects:copper-spring') {
    ext.moduleName = 'org.copperengine.spring'
    dependencies {
        implementation project(':projects:copper-coreengine')

        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"

        // Spring
        implementation "org.springframework:spring-aop:$springVersion"
        implementation "org.springframework:spring-beans:$springVersion"
        implementation "org.springframework:spring-context:$springVersion"
        implementation "org.springframework:spring-core:$springVersion"
        implementation "org.springframework:spring-expression:$springVersion"
        implementation "org.springframework:spring-jdbc:$springVersion"
        implementation "org.springframework:spring-tx:$springVersion"

        implementation "org.springframework.batch:spring-batch-infrastructure:$springBatchVersion"
    }
}


project(':projects:copper-coreengine') {
    ext.moduleName = 'org.copperengine.core'
    dependencies {
        api project(':projects:copper-jmx-interface')

        api "org.slf4j:slf4j-api:$slf4jVersion"

        // asm
        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-commons:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "org.ow2.asm:asm-util:$asmVersion"
        implementation "org.ow2.asm:asm-analysis:$asmVersion"
    }

    tasks.register('scriptsZip', Zip) {
        archiveClassifier = 'scripts'
        from file("src/main/database")
        into 'scripts/sql'
    }
    assemble.dependsOn scriptsZip

    artifacts {
        archives scriptsZip
    }
}

project(':projects:copper-performance-test') {
    ext.moduleName = 'org.copperengine.performancetest'
    dependencies {
        implementation project(':projects:copper-coreengine')
        implementation project(':projects:copper-ext')
        implementation project(':projects:copper-cassandra:cassandra-storage')

        implementation "org.ow2.asm:asm:$asmVersion"
        implementation "org.ow2.asm:asm-tree:$asmVersion"
        implementation "org.yaml:snakeyaml:$snakeyamlVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "mysql:mysql-connector-java:$mysqlVersion"
        implementation "org.apache.derby:derby:$derbyVersion"
        implementation "postgresql:postgresql:$postgresqlVersion"
        implementation "com.h2database:h2:$h2Version"
        implementation "com.mchange:c3p0:$c3p0Version"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        runtimeOnly fileTree(dir: "$rootDir/3rdPartyLibs", include: '*.jar')
    }

    jar {
        dependsOn ':projects:copper-coreengine:jar', ':projects:copper-ext:jar', ':projects:copper-cassandra:cassandra-storage:jar', ':projects:copper-jmx-interface:jar'
        manifest.attributes provider: 'gradle'

        //name = "copper-performance-test.jar"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from {
            configurations.runtimeClasspath.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        manifest {
            attributes 'Main-Class': 'org.copperengine.performancetest.main.Main'
        }

    }
}
